buildscript {
    repositories { 
        mavenLocal()
        jcenter()
        mavenCentral()
    }
	repositories {
      maven {
        url "https://plugins.gradle.org/m2/"
      }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.2.2.RELEASE")
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.9.2'
        classpath 'net.researchgate:gradle-release:2.6.0'
    }
}

plugins { 
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id "io.spring.dependency-management" version "1.0.8.RELEASE" 
    id "com.github.kt3k.coveralls" version "2.8.1" 
    id "org.owasp.dependencycheck" version "3.1.2"
    id "org.asciidoctor.convert" version "1.5.3"
    id "com.gorylenko.gradle-git-properties" version "1.4.17"
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'jacoco'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

println "Building ${name} version: ${version}"

jar {
    baseName = 'collection-api'
    version = System.getenv('version')
}

repositories {
    mavenLocal() 
    jcenter()
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    all*.exclude module : 'spring-boot-starter-logging'
}

lombok {
    version = '1.18.10'
    sha256 = ""
}

if (project.hasProperty('travis')) {
    println 'Using travis profile for building ' + project.getName()
    apply from: 'gradle/profile-travis.gradle'
} else if (project.hasProperty('release') || project.hasProperty('clean-release')) {
    println 'Using release profile for building ' + project.getName()
    apply from: 'gradle/profile-release.gradle'   
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-data-rest")
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    api("org.springdoc:springdoc-openapi-data-rest:1.2.31")
  
    implementation("edu.kit.datamanager:service-base:0.2.0"){
        exclude group: 'org.javers', module: 'javers-spring-boot-starter-sql'
    }
    implementation("de.codecentric:spring-boot-admin-starter-client:2.1.6")

    implementation("org.postgresql:postgresql:42.2.1")
    implementation("com.h2database:h2:1.4.194")

    //Java 11 Support 
    implementation('javax.xml.bind:jaxb-api:2.2.11')
    implementation('com.sun.xml.bind:jaxb-core:2.2.11')
    implementation('com.sun.xml.bind:jaxb-impl:2.2.11')
    implementation('javax.activation:activation:1.1.1')
    implementation('org.javassist:javassist:3.23.1-GA')
    
    implementation('org.apache.httpcomponents:httpclient:4.5.6')

    testImplementation("com.h2database:h2:1.4.194") 
    testImplementation("org.springframework.restdocs:spring-restdocs-mockmvc")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.springframework:spring-test")
    testImplementation("org.springframework.security:spring-security-test")
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    compile ('org.springframework.boot:spring-boot-starter-thymeleaf')
   
    //Java 11 Support 
    testImplementation("org.mockito:mockito-core:2.18.0")
    testImplementation("org.powermock:powermock-module-junit4:2.0.0-beta.5")
    testImplementation("org.powermock:powermock-api-mockito2:2.0.0-beta.5")

    testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc:2.0.2.RELEASE')
    testImplementation('org.asciidoctor:asciidoctor-gradle-plugin:1.5.9.2')
}

springBoot  {
    buildInfo()
}

bootJar {
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    launchScript()
    archiveFileName = "${archiveBaseName.get()}-${archiveVersion.get()}.${archiveExtension.get()}"
}

test {
    testLogging {
        outputs.upToDateWhen {false}
        showStandardStreams = true
    }
}


jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    afterEvaluate {
        //exclude some classes/package from code coverage report
        classDirectories.setFrom(files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: [\
            'edu/kit/datamanager/collection/configuration/**', \
            'edu/kit/datamanager/collection/filters/**', \
            'edu/kit/datamanager/collection/Application*'
                        ])
                }))
    }
}
